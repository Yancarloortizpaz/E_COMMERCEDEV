CREATE OR ALTER PROCEDURE [SQM_CATALOGS].[sp_ProductVariableTypes_Create]
(
    @productVariableTypeName VARCHAR(50),
    @productVariableTypeDescription VARCHAR(100),
    @productVariableTypeCreatorId INT,
    @productVariableTypeStatusId BIT,
    @o_code INT = NULL OUTPUT,
    @o_message VARCHAR(255) = NULL OUTPUT,
    @o_templateId INT = NULL OUTPUT
)
AS
BEGIN
    SET NOCOUNT ON;
    SET XACT_ABORT ON;

    BEGIN TRY
        BEGIN TRANSACTION;

        IF EXISTS
        (
            SELECT 1
            FROM [SQM_CATALOGS].[Tbl_ProductVariableTypes]
            WHERE productVariableTypeName = LTRIM(RTRIM(@productVariableTypeName))
        )
        BEGIN
            SET @o_code = 409;
            SET @o_message = 'El tipo de variable ya existe.';
            ROLLBACK TRANSACTION;
            RETURN;
        END

        INSERT INTO [SQM_CATALOGS].[Tbl_ProductVariableTypes]
        (
            productVariableTypeName,
            productVariableTypeDescription,
            productVariableTypeCreatorId,
            productVariableTypeCreationDate,
            productVariableTypeStatusId
        )
        VALUES
        (
            LTRIM(RTRIM(@productVariableTypeName)),
            LTRIM(RTRIM(@productVariableTypeDescription)),
            @productVariableTypeCreatorId,
            GETDATE(),
            @productVariableTypeStatusId
        );

        SET @o_templateId = SCOPE_IDENTITY();

        COMMIT TRANSACTION;

        SET @o_code = 200;
        SET @o_message = 'Registro creado exitosamente.';
    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION;

        SET @o_code = ERROR_NUMBER();
        SET @o_message = ERROR_MESSAGE();
    END CATCH
END;
GO